::
  @ctxt\RequestContext\$

:TEMPLATE = 'static/filelist.html'

:reply
  // Check if the path exists.
  target_dir := _path.join('data', @ctxt.path)
  if !_path.existsSync(target_dir)
    @ctxt.response(404, target_dir)
    =>

  // Load the template file.
  if !_path.existsSync(%.ListHandler.TEMPLATE)
    @ctxt.response(500, %.ListHandler.TEMPLATE + ' not found')
    =>
  tmpl := _fs.readFileSync(%.ListHandler.TEMPLATE, 'utf8')

  // Get the file list and make a new file name.
  file_list := _fs.readdirSync(target_dir)
  generator := &NameGenerator()
  newname := generator.nextName()
  while _path.existsSync(_path.join('data', newname))
    newname = generator.nextName()

  // Render.
  @ctxt.tryWithResponse(##)
    html := _ejs.render(tmpl, {#})
      path: target_dir
      files: file_list
      newname: newname

    @ctxt.res.writeHead(200, {'Content-Type': 'text/html'})
    @ctxt.res.write(html)
