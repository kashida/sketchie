::
  @ctxt\Context\$
  @handlers\HandlerSet\$

:SCRIPT_LOAD = '<script>run_scrawler();</script>'
:OPEN_MARKER = '<!--%'
:CLOSE_MARKER = '%-->'

:post
  // TODO: handle sub-dirs.
  data := ''
  @ctxt.req.setEncoding('utf8')
  @ctxt.req.on('data', ##)
    chunk\s\$
    data += chunk
  @ctxt.req.once('end', ##)
    @handlers.pageRender.saveAndReply(#)
      {contents: data}
      _path.join('data', @ctxt.path)

:@packageList\s\
  => #+
    '<script>\n'
    &Compile('client').pkgList().map(##).join('')
      line\s\$
      => line + '\n'
    '</script>\n'

:@scriptList\s\
  additional_scripts\A.<s>\? []
  list := &Compile('client').scriptList.map(##).join('')
    file\s\$
    => '<script src="/_s/' + file + '"></script>\n'
  if additional_scripts.length
    list += additional_scripts.map(##).join('')
      file\s\$
      => '<script src="/_ts/' + file + '"></script>\n'
  => list

Try to render. If fail, return the input as is (and log the error message).
:@tryReplace\s\
  tmpl\s\$
  opt\O\$
  try
    => _ejs.render(tmpl, opt)
  catch e
    log(e.stack)
    => tmpl

:replaceScript\s\
  html\s\$
  => @tryReplace(html, {#})
    open: %.HtmlHandler.OPEN_MARKER
    close: %.HtmlHandler.CLOSE_MARKER
    script: #+
      @packageList()
      @scriptList()
    init: %.HtmlHandler.SCRIPT_LOAD

:create
  additional_scripts\A.<s>\?
  additional_init\s\? ''
  @handlers.pageRender.reply([{contents: ''}, {#}])
    open: %.HtmlHandler.OPEN_MARKER
    close: %.HtmlHandler.CLOSE_MARKER
    script: #+
      @packageList()
      @scriptList(additional_scripts)
    init: %.HtmlHandler.SCRIPT_LOAD + additional_init
