Item list mode parameter adjustments.

::<%.Palette
  @blend_alpha\ui.Slider\ &ui.Slider('item_alpha', 'blend alpha%', 0, 100, 100, 20)
  @alpha_lock\Element\ dom.id('image_alph_alock')
  @blend_alpha.callbackUpdate = ##
    @updateBlendAlpha()
  ^('item_palette', {i: 'image_section'})
  dom.listen(@alpha_lock, 'change', ##)
    @updateAlphaLock()

:blendAlphaAdjust
  incr\n\$
  @blend_alpha.adjust(incr)

:toggleAlphaLock
  img\Image\?
  if !img
    if !f_page.selection.isHeadImage
      =>
    img = f_page.selection.head.img
  img.alphaLock = !img.alphaLock
  @alpha_lock.checked = f_page.selection.head.alphaLock

:@updateBlendAlpha
  // Update selected item's state, based on control.
  sel := f_page.selection.head
  if !sel
    =>
  sel.blendAlpha = @blend_alpha.value
  sel.style.opacity = @blend_alpha.value / 100

:@updateAlphaLock
  // Update selected item's state, based on control.
  if !f_page.selection.isHeadImage
    =>
  dom.setAttr(#)
    f_page.selection.head.element
    'alpha_lock'
    @alpha_lock.checked ? 'true' : undefined

:update
  // Update the control based on the seleted item's state.
  if !f_page.selection.head
    @.close()
    =>
  blend_alpha := f_page.selection.head.blendAlpha
  if blend_alpha
    @blend_alpha.setValue(blend_alpha)
  else
    @blend_alpha.reset()

  if f_page.selection.isHeadImage
    @alpha_lock.checked = f_page.selection.head.alphaLock
    @.openSection('i')
  else
    @.closeSection('i')
