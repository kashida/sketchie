::^~Palette
  @mix\\ $ui.Slider('color_mix', 'mix%', 0, 100, 0, 10)
  @hue\\ $ui.Slider('color_hue', 'hue%', -300, 300, 0, 10)
  @sat\\ $ui.Slider('color_sat', 'sat%', -100, 100, 0, 20)
  @lgt\\ $ui.Slider('color_lgt', 'lgt%', -100, 100, 0, 40)
  @color_sel\Element|$data.Hsl\ &$data.Hsl(0, 0, 0)
  @prev_color\\ $data.Hsl(0, 0, 0)
  ^('color_palette')
  update_cb := ##
    @update()
  @mix.callbackUpdate(update_cb)
  @mix.callbackRender(##)
    val\n\$
    => @valueRender({m:val / 100})
  @hue.callbackUpdate(update_cb)
  @hue.callbackRender(##)
    val\n\$
    => @valueRender({h:val / 100})
  @sat.callbackUpdate(update_cb)
  @sat.callbackRender(##)
    val\n\$
    => @valueRender({s:val / 100})
  @lgt.callbackUpdate(update_cb)
  @lgt.callbackRender(##)
    val\n\$
    => @valueRender({l:val / 100})
  @createColorSelectors()

:mixAdjust
  incr\n\$
  @mix.adjust(incr)

:hueAdjust
  incr\n\$
  @hue.adjust(incr)

:satAdjust
  incr\n\$
  @sat.adjust(incr)

:lgtAdjust
  incr\n\$
  @lgt.adjust(incr)

:activateControl
  p\s\$
  (\$ui.Slider\({m: @mix, h:@hue, s:@sat, l:@lgt}[p])).activate()

:unselect
  if @color_sel instanceof Element
    @color_sel.classList.remove('selected')

:select
  if @color_sel instanceof Element
    @color_sel.classList.add('selected')

:@currentColor\$data.Hsl\
  if @color_sel instanceof Element
    => @color_sel.hsl
  else if @color_sel instanceof $data.Hsl
    => @color_sel
  else
    => &$data.Hsl(0, 0, 0)
  
:selectedColor+\$data.Color\
  params := @colorAdjustParams()
  => @adjustedColor(#)
    params[0]
    params[1]
    params[2]
    params[3]

:@colorAdjustParams
  => [#]
    @hue.value / 100
    @sat.value / 100
    @lgt.value / 100
    @mix.value / 100

:@adjustedColor\$data.Color\
  adj_h\n\$
  adj_s\n\$
  adj_l\n\$
  adj_m\n\$
  cc := @currentColor()
  a := f_tool_palette ? f_tool_palette.alpha() / 100.0 : 1
  h := (1.0 - adj_m) * cc.h + adj_m * @prev_color.h
  s := (1.0 - adj_m) * cc.s + adj_m * @prev_color.s
  l := (1.0 - adj_m) * cc.l + adj_m * @prev_color.l
  if h
    h += adj_h
  s += adj_s
  l += adj_l
  => &$data.Hsl(h, s, l, a).color

:@valueRender
  params\O\$
  cap := @colorAdjustParams()
  ah := cap[0]
  as := cap[1]
  al := cap[2]
  am := cap[3]
  if params.h
    ah = params.h
  if params.s
    as = params.s
  if params.l
    al = params.l
  if params.m
    am = params.m
  col := @adjustedColor(ah, as, al, am)
  => {c:col.rgb}

:setColorRgb
  c\$data.Color\$
  @setColor(c.hsl)

:@setColor
  hsl\$data.Hsl\$
  @mix.reset()
  @hue.reset()
  @sat.reset()
  @lgt.reset()
  @prev_color = @currentColor()
  if @color_sel instanceof Element
    @color_sel.classList.remove('selected')
  @color_sel = hsl
  $dom.id('color_name').innerText = '---'
  @update()

:swapColors
  @setColor(@prev_color)

:@createColorSelectors
  colors := [#]
    ['40404F', 'Paynes gray']
    ['36454F', 'Charcoal']
    ['708090', 'Slate gray']
    ['F0EAD6', 'Eggshell']
    ['007FFF', 'Azure']
    ['0047AB', 'Cobalt blue']
    //['1034A6', 'Egyptian blue']
    //['1C39BB', 'Persian blue']
    ['120A8F', 'Ultramarine']
    ['4B0082', 'Indigo']
    ['B57EDC', 'Lavender']
    ['E0B0FF', 'Meuve']
    ['C8A2C8', 'Lilac']
    ['FF00FF', 'Fuchsia']
    ['DA70D6', 'Orchid']
    ['8E4585', 'Plum']
    ['614051', 'Eggplant']
    //['66424D', 'Tuscan red']
    ['722F37', 'Wine']
    ['960018', 'Carmine']
    //['92000A', 'Sangria']
    //['800000', 'Maroon']
    ['DC143C', 'Crimson']
    //['CC3333', 'Persian red']
    ['E34234', 'Vermilion']
    ['FA8072', 'Salmon']
    ['8A3324', 'Burnt umber']
    //['954535', 'Chestnut']
    ['882D17', 'Sienna']
    ['FF7F50', 'Coral']
    //['CB410B', 'Sinopia']
    ['C04000', 'Mahogany']
    ['EC5800', 'Persimmon']
    ['FF7518', 'Pumpkin']
    ['CC7722', 'Ochre']
    ['704214', 'Sepia']
    ['7B3F00', 'Chocolate']
    ['ED9121', 'Carrot orange']
    //['E49B0F', 'Gamboge']
    ['FFBF00', 'Amber']
    ['FFF700', 'Lemon']
    ['808000', 'Olive']
    ['D1E231', 'Pear']
    ['BFFF00', 'Lime']
    ['228B22', 'Forest green']
    ['50C878', 'Emerald']
    ['3EB489', 'Mint']
    ['40826D', 'Viridian']
    ['40E0D0', 'Turquoise']
    ['007BA7', 'Cerulean']
  //verify = &VerifyColors()
  cname := $dom.id('color_name')
  csel := $dom.id('color_sel')
  colors.forEach(##)
    c\A\$
    i\n\$
    //verify.check(c[0], c[1])
    elem := $dom.create('div')
    elem.color_name = c[1]
    elem.style['background-color'] = '#' + c[0]
    elem.classList.remove('selected')
    elem.hsl = $data.Color.fromHexString(c[0]).hsl
    // TODO: make just one listener for all the div's.
    $dom.listen(elem, 'click', ##)
      evt\Event\$
      @mix.reset()
      @hue.reset()
      @sat.reset()
      @lgt.reset()
      @prev_color = @currentColor()
      if @color_sel instanceof Element
        @color_sel.classList.remove('selected')
      @color_sel = \Element\(evt.target)
      cname.innerText = @color_sel.color_name
      @color_sel.classList.add('selected')
      @update()
    csel.appendChild(elem)
    if i == 0
      // Select the first one.
      cname.innerText = c[1]
      elem.classList.add('selected')
      @color_sel = elem
      @prev_color = @currentColor()
  @update()

:@update
  $dom.id('color_sample0').style['background-color'] = @.selectedColor.rgb
  $dom.id('color_sample1').style['background-color'] = @prev_color.color.rgb

::VerifyColors
  @last_rgb\A<n>|-\

:check
  col\$data.Color\$
  name\s\$
  rgb := col.array
  if @last_rgb
    distsq := 0
    [0, 1, 2].forEach(##)
      i\n\$
      d := rgb[i] - @last_rgb[i]
      distsq += d * d
    l(name + ':' + distsq)
  @last_rgb = rgb
