Global components of the app -- i.e. only one instance in the lifetime of the app.
:
  f_scrawler := null
  f_viewport := null
  f_item_list := null
  f_surface := null
  f_stroke_layer := null
  f_color_palette := null
  f_tool_palette := null
  f_item_palette := null
  f_cursor := null
  f_dispatcher := null
  f_editor_modes := null
  f_hex_control := null
  f_z_order := null
  f_strokes := null

::Scrawler
  @run_as_extension\b\$
  @build_dom()
  @init_global_components()

:@init_global_components
  f_scrawler = this
  f_viewport = &Viewport()
  f_item_list = &item.List()
  item_selection := &item.Selection()
  f_z_order = &item.ZOrder()
  f_surface = &Surface(id('surface'))
  f_stroke_layer = &Surface(id('stroke_layer'))
  f_color_palette = &palette.Color()
  f_tool_palette = &palette.Tool()
  f_item_palette = &palette.Item(item_selection)
  f_cursor = &Cursor()

  textEditor := &editor.Text(item_selection)
  f_editor_modes = &editor.Modes('l', {#})
    'l': &editor.List(item_selection, textEditor)
    't': textEditor
    'r': &editor.Rotate(item_selection)
    'q': &editor.Scale(item_selection)
    'd': &editor.Draw()
    's': &editor.Stencil()
  f_dispatcher = &Dispatcher()
  &KeyDispatcher()
  f_hex_control = &HexControl()
  f_strokes = &stroke.Set()

upload: ->
  chrome.extension.sendRequest ['list'], (resp) ->
    l resp

Server access.
:upload
  // Send everything in the item list.
  html := #+
    '<!-- EDITOR_MARKER -->'
    '<div class="itemlist">'
    id('itemlist').innerHTML
    '</div>'
  if @run_as_extension
    bb := &WebKitBlobBuilder()
    bb.append(save_header)
    bb.append(html)
    bb.append(save_footer)
    //getURL = window.createBlobURL || window.createObjectURL
    ifr := id('save_ifr')
    if !ifr
      ifr = create('iframe')
      ifr.id = 'save_ifr'
      ifr.style.display = 'none'
    ifr.src = window.webkitURL.createObjectURL(bb.getBlob())
    document.body.appendChild(ifr)
  else
    xhr_post(location.pathname, html)

:@build_dom
  appendChildren(document.body, [#])
    findOrCreate('div', {class: 'itemlist'})
    create('canvas', {class: 'surface'})
    create('canvas', {class: 'stroke_layer'})
    createDiv('draw_cursor')
    svgCreate('svg', {class: 'control_root'})
    createDiv('input_layer')
    create('form', {}, [createDiv('palette_container', [#])])
      createDiv(['tool_palette', 'palette'], [#])
        createDiv('sample_box', [#])
          create('canvas', {class: 'tool_sample'})
          createDiv('sample_size')
        createDiv('tool_lerp')
        createDiv('tool_alpha')
        createDiv('tool_radius')
      createDiv(['color_palette', 'palette'], [#])
        createDiv('color_sel')
        createDiv('box', [#])
          createDiv(['color_sample0', 'color_sample'])
          createDiv(['color_sample1', 'color_sample'])
          createDiv('color_mix')
          createDiv('color_hue')
          createDiv('color_sat')
          createDiv('color_lgt')
        createDiv('color_name')
      createDiv(['item_palette', 'palette'], [#])
        createDiv('box', [#])
          createDiv('item_alpha')
        createDiv(['image_section', 'palette'], [#])
          createDiv('box', [#])
            createDiv('box', [#])
              create('input', {class: 'image_alph_alock', type: 'checkbox'})
              createText('alpha lock')
    createDiv('img_undolist')

:'save_header
  <!doctype html>
  <html><head>
    <title>Scrawler</title>
    <link rel="stylesheet" href="scrawler.css" type="text/css">
    </head><body>

:'save_footer
  </body></html>

Entry point.
:
  run_scrawler := ##
    run_as_extension\b\? false
    => &Scrawler(run_as_extension)
