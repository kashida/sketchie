::
  @element\Element\$
  @position+\\ $data.Vec2(0.0, 0.0)
  @rotation+\n\ 0.0
  @scale+\n\ 1.0

:loadFromElement
  element\Element\$
  @.position = \$data.Vec2\($data.Vec2.fromStr(~attr(element, 'sk_position')))
  @.rotation = ~attr(element, 'sk_rotation') || 0
  s := ~attr(element, 'sk_scale')
  if s != null && s != ""
    @.scale = parseFloat(s)

:freeze
  ~setAttr(@element, 'sk_position', @position.str)
  if @rotation != 0
    ~setAttr(@element, 'sk_rotation', @rotation.toString())
  if @scale != 1.0
    ~setAttr(@element, 'sk_scale', @scale.toString())
  xform := @makeXform()
  // Need to set as a style attribute. Setting as element.style[property] will
  // not work for the styles that Chrome doesn't understand.
  ~setAttr(@element, 'style', [#].join('; '))
    'transform: ' + xform
    '-ms-transform: ' + xform
    '-webkit-transform: ' + xform

:restore
  ~setAttr(@element, 'sk_position')
  ~setAttr(@element, 'sk_alpha_lock')
  ~setAttr(@element, 'sk_alpha_blend')
  @setXform()

Document coords position, bottom-right position, and bounding-box.
:centerPosition+\$data.Vec2\
  => ~elemRect(@element.firstElementChild).center.plus(@position)
:positionBR+\$data.Vec2\
  => ~elemPositionBR(@element.firstElementChild).plus(@position)
:bbox+\$data.Rect\
  => @.rect.plus(@position)

Item coords rect.
:rect+\$data.Rect\
  => ~elemRect(@element.firstElementChild)

:set
  s\n\$
  r\n\$
  x\n\$
  y\n\$
  @scale = s
  @rotation = r
  @position.x = x
  @position.y = y
  @setXform()

:position*
  @position = value
  @setXform()

:rotation*
  @rotation = value
  @setXform()

:scale*
  @scale = value
  @setXform()

:@makeXform\s\
  xforms := []
  if @position.x != 0 || @position.y != 0
    xforms.push('translate(' + @position.x + 'px,' + @position.y + 'px)')
  if @rotation
    xforms.push('rotate(' + @rotation + 'rad)')
  if @scale != 1.0
    xforms.push('scale(' + @scale + ')')
  => xforms.join(' ')

:@setXform
  @element.style['-webkit-transform'] = @makeXform()

:@clearXform
  @element.style['-webkit-transform'] = ''

:enableXform*
  enable\b\$
  if enable
    @setXform()
  else
    @clearXform()
