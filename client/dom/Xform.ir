::
  @element\!Element\$
  @position+\data.Vec2\ &data.Vec2(0.0, 0.0)
  @rotation+\n\ 0.0
  @scale+\n\ 1.0

Creates a new xform detached from the element.
:snapshot+\!%.XformSnapshot\
  => &%.XformSnapshot(@)

:centerPosition+\!data.Vec2\
  => dom.elemRect(@element).center.plus(@position)
:positionBR+\!data.Vec2\
  => dom.elemPositionBR(@element).plus(@position)
:rect+\!data.Rect\
  => dom.elemRect(@element)

:set
  s\n\$
  r\n\$
  x\n\$
  y\n\$
  @scale = s
  @rotation = r
  @position.x = x
  @position.y = y
  @setXform()

:position*
  @position = value
  @setXform()

:centerPosition*
  value\!data.Vec2\$
  @position = value.minus(dom.elemPositionBR(@element).div(2.0))
  @setXform()

:rotation*
  @rotation = value
  @setXform()

:scale*
  @scale = value
  @setXform()

:@setXform
  xforms := []
  if @position.x != 0 || @position.y != 0
    xforms.push('translate(' + @position.x + 'px,' + @position.y + 'px)')
  if @rotation
    xforms.push('rotate(' + @rotation + 'rad)')
  if @scale != 1.0
    xforms.push('scale(' + @scale + ')')
  @element.style['-webkit-transform'] = xforms.join(' ')

:@clearXform
  @element.style['-webkit-transform'] = ''

:enableXform*
  enable\b\$
  if enable
    @setXform()
  else
    @clearXform()
