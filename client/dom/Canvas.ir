Convenience wrapper for the html canvas.
::
  // If two values, (width, height).  if one value, (canvas_element).
  // x and y, if provided, specify the logical position of this canvas in
  // document coords.
  canvas_or_w\HTMLCanvasElement|n\$
  h\n\?
  @element+\HTMLCanvasElement\
  @ctx+\CanvasRenderingContext2D\
  @scale\n\ 1.0
  if h
    @element = %.create('canvas')
    @element.width = canvas_or_w
    @element.height = h
  else
    @element = \HTMLCanvasElement\(canvas_or_w)
  @ctx = @element.getContext('2d')

:opacity*
  val\?s|n\$
  @element.style.opacity = val

:show
  @element.style.display = 'block'

:hide
  @element.style.display = 'none'

:clear
  @ctx.clearRect(0, 0, @element.width, @element.height)

:dataUrl
  => @element.toDataURL()

:scale*
  @scale = value
  @element.width = @element.clientWidth / @scale
  @element.height = @element.clientHeight / @scale

:pixelData+\!pixel.Data\
  => &pixel.Data(@ctx.getImageData(0, 0, @element.width, @element.height))

:duplicate\!HTMLCanvasElement\
  cvs := %.create('canvas')
  cvs.width = @element.width
  cvs.height = @element.height
  cvs.getContext('2d').putImageData(@.pixelData.pixels, 0, 0)
  => cvs

:copyFrom
  cvs\!HTMLCanvasElement\$
  @ctx.putImageData(cvs.getContext('2d').getImageData(#), 0, 0)
    0
    0
    @element.width
    @element.height

Attaching a canvas to div leaves the canvas's coord system to be parented to
the div even when the canvas is position:fixed. Switching to position:absolute
and counter the xform.
TODO: need to counter other item transforms and viewport transforms.
:setToAbsolute
  p\data.Vec2\$
  @element.style.position = 'absolute'
  @element.style.width = document.width + 'px'
  @element.style.height = document.height + 'px'
  @element.style.left = -p.x + 'px'
  @element.style.top = -p.y + 'px'

:detach
  @element.parentNode.removeChild(@element)
  @element.style.position = undefined
  @element.style.width = undefined
  @element.style.height = undefined
  @element.style.left = undefined
  @element.style.top = undefined
