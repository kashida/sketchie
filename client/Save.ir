::

:json+\!Blob\
  => JSON.stringify(f_page.list.serializable)

:jsonBlob+\!Blob\
  => &Blob([@.json], {type: 'application/json'})

:html+\s\
  => Templates.page({#})
    editor: false
    script: ''
    init: ''
    contents: #+
      '<div class="itemlist">'
      dom.id('itemlist').innerHTML
      '</div>\n'

:htmlBlob+\!Blob\
  => &Blob([@.html], {type: 'text/html'})

:store
  // Send everything in the item list.
  if run_as_extension
    @storeToSyncFs('sketchie.sktch', @.jsonBlob)
    @storeToSyncFs('sketchie.html', @.htmlBlob)
  else
    xhrPost(location.pathname.replace(#), @.json)
      /\.html$/i
      '.sktch'
    xhrPost(location.pathname, @.html)

:exportLocal
  chrome.fileSystem.chooseEntry({type: 'saveFile'}, ##)
    entry\Entry\?
    if entry
      entry.createWriter(##, @err)
        writer\FileWriter\$
        writer.onerror = @writererr
        writer.onwriteend = ##
          // TODO: show some on-screen response.
          l('write complete')
        writer.write(@.htmlBlob)

Sync to local file system -- currently not used.
:@storeToFs
  chrome.fileSystem.chooseEntry({type: 'openFile'}, ##)
    entry\Entry\?
    if entry
      entry.file(##)
        file\!File\$
        reader := &FileReader()
        reader.onerror = @writererr
        reader.onloadend = ##
          e\Event\$
          l(e.target.result)
        l(reader.readAsText(file))

:@storeToSyncFs
  fname\s\$
  data\!Blob\$
  chrome.syncFileSystem.requestFileSystem(##)
  //webkitRequestFileSystem(window.PERSISTENT, 5 * 1024 * 1024, ##, @err)
    fs\FileSystem\$
    //chrome.syncFileSystem.getUsageAndQuota(fs, ##)
    //  info\O\$
    //  l(info)
    fs.root.getFile(fname, {create: true}, ##, @err)
      entry\FileEntry\$
      entry.createWriter(##, @err)
        writer\FileWriter\$
        writer.onerror = @writererr
        writer.onwriteend = ##
          if writer.length == 0
            writer.write(data)
        writer.truncate(0)

:@writererr
  e\ProgressEvent\$
  l('Writer error')
  l(e)

:@err
  e\FileError\$
  l('Save error')
  l(e)
