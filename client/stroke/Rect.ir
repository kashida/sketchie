::< %.Stroke
  ^()

:reset
  color\A\$
  radius\n\$
  lerp\n\$
  alpha_lock\b\$
  ^(color, radius, lerp, alpha_lock)
  @first_x = @first_y = @last_x = @last_y = null

:draw
  x\n\$
  y\n\$
  f_stroke_layer.clear()
  @last_x = x
  @last_y = y
  if !@first_x || !@first_y
    @first_x = x
    @first_y = y
    =>
  @draw_rect(f_stroke_layer.ctx, false)

:blit
  @draw_rect(f_surface.ctx, true)

:@draw_rect
  ctx\CanvasRenderingContext2D\$
  fill\b\$
  if !@first_x || !@first_y || !@last_x || !@last_y
    =>
  if @first_x == @last_x || @first_y == @last_y
    =>
  if fill
    ctx.fillStyle = @c.rgba
  else
    ctx.strokeStyle = @c.rgba
  x0 := @first_x
  x1 := @last_x
  y0 := @first_y
  y1 := @last_y
  if @last_x < @first_x
    x0 = @last_x
    x1 = @first_x
  if @last_y < @first_y
    y0 = @last_y
    y1 = @first_y
  if fill
    ctx.fillRect(x0, y0, x1 - x0, y1 - y0)
  else
    ctx.strokeRect(x0, y0, x1 - x0, y1 - y0)
