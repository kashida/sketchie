::<%.Editor
  ^()

:@holdSelection
  f_page.selection.list.forEach(##)
    item\model.Item\$
    item.isActive = false
    item.style['pointer-events'] = 'none'
  f_page.selection.clear()

:@unholdAll
  f_page.list.each(##)
    item\model.Item\$
    item.isActive = true
    item.style['pointer-events'] = null

------------------------------------------------------------
Image methods.

:pasteImage
  evt\Event\$
  // Get the image from clipboard.
  url := evt.clipboardData.getData('text/plain')
  if !url
    // The image is copied as html, not as url.
    html := evt.clipboardData.getData('text/html')
    if !html
      =>
    div := dom.create('div')
    div.innerHTML = html
    img := div.getElementsByTagName('img')[0]
    if !img
      =>
    url = img.src
  img = &pixel.PositionedPixels(#).makeImageItem()
    &data.Vec2(window.pageXOffset, window.pageYOffset)
    url
  img.isReference = true
  f_page.list.appendEnd(img)


------------------------------------------------------------
Key handler.

:dispatch
  evt\Event\$
  shift := 0x100
  key := evt.keyCode
  if evt.shiftKey
    key += shift

  // Selection control.
  if key == c('H')
    @holdSelection()
  else if key == c('H') + shift
    @unholdAll()

  // z control.
  else if key == c('V')
    f_z_order.moveToEnd(f_page.selection.list, true)
  else if key == c('V') + shift
    f_z_order.moveToEnd(f_page.selection.list, false)
  else if key == c('N')
    f_z_order.shiftOneWithOverlap(false)
  else if key == c('N') + shift
    f_z_order.shiftOneWithOverlap(true)

  // Transform.
  else if key == c('R')
    f_item_palette.close()
    f_editor_modes.changeModeTo('r')
  else if key == c('T')
    f_item_palette.close()
    f_editor_modes.changeModeTo('q')

  // Open / close the control palette.
  else if key == c(' ')
    f_item_palette.toggle()

  // Entering to the text mode.
  else if key == c('I')
    f_editor_modes.changeModeTo('ti')
  else if key == c('E')
    f_editor_modes.changeModeTo('te')

  // Draw / stencil mode change.
  else if key == c('S')
    f_editor_modes.changeModeTo('s')

  // Mark image alpha locked.
  else if key == c('X')
    f_item_palette.toggleAlphaLock()

  // Adjust the image blend alpha.
  else if key == DOM_VK_COMMA + shift || key == DOM_VK_PERIOD + shift
    f_item_palette.blendAlphaAdjust(evt.keyCode == DOM_VK_PERIOD ? 1 : -1)

  // Viewport control.
  else if key == DOM_VK_LEFT
    f_viewport.move(-1, 0)
  else if key == DOM_VK_UP
    f_viewport.move(0, -1)
  else if key == DOM_VK_RIGHT
    f_viewport.move(1, 0)
  else if key == DOM_VK_DOWN
    f_viewport.move(0, 1)

  f_global_actions.dispatch(\KeyboardEvent\(evt))
