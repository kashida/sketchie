::<%.Item
  el\!Element\?
  @textEl+\!Element\ dom.createDiv([])
  @onblur\F\
  @editing\b\ false
  ^(el)
  @.element.appendChild(@textEl)

:dispose
  @unlisten()
  ^()

:serializable+\O\
  obj := @.baseSerializable
  obj.text = @.text
  => obj

:createFromObj = ##
  \%.Text\
  obj\!O\$
  if !obj.text
    => null
  txt := &%.Text()
  txt.text = obj.text
  => txt

:createFromElement = ##
  \!%.Text\
  element\!Element\$
  txt := &%.Text()
  txt.text = element.innerText
  => txt

:text+\s\
  => @textEl.innerText
:text*
  str\s\$
  @.emptyChildren()
  @textEl.appendChild(@makeMinimumTextDiv(str))

:<selectable
  => ^() && !@editing

:editable*
  e\b\$
  if !e
    @unlisten()
  @editing = e
  @textEl.contentEditable = e
  @textEl.style['-webkit-user-select'] = e ? 'text' : 'none'

:emptyChildren
  @textEl.innerHTML = ''

TODO: Normalize to a smaller dom tree, e.g. div around br isn't necessary.
  We can also use <p> for each line for the consistency (no div or br).
:@makeMinimumTextDiv
  str\s\? ''
  div := dom.create('div')
  str = str.trim()
  if !str
    div.appendChild(dom.create('br'))
    => div

  lines := str.split('\n')
  lines.forEach(##)
    line\s\$
    index\n\$
    div.appendChild(dom.createText(line))
    if index < lines.length - 1
      brdiv := dom.create('div')
      brdiv.appendChild(dom.create('br'))
      div.appendChild(brdiv)
  => div

Make sure there is at least one div left when there is backspace in text
edit mode.
:backspace
  el:= @textEl
  el.normalize()
  if el.childNodes.length == 1 && el.firstChild.tagName == 'BR'
    el.removeChild(el.firstChild)
  if el.childNodes.length != 0
    =>
  el.appendChild(@makeMinimumTextDiv())
  window.getSelection().collapse(el, 0)

:setCursor
  beginning\b\$
  onblur\F\?
  window.getSelection().collapse(#)
    @textEl
    beginning ? 0 : @textEl.innerText.length
  @.focus()
  if onblur
    @unlisten()
    @onblur = onblur
    dom.listen(@textEl, 'blur', @onblur)

:@unlisten
  if @onblur
    dom.unlisten(@textEl, 'blur', @onblur)

:moveChildrenFrom
  other\%.Text\$
  start\n\? 0
  i := start
  while i < other.textEl.childNodes.length
    @textEl.appendChild(other.textEl.childNodes[i])

:@trimEndWhitespace
  str\s\$
  => str.replace(/^[\r\n]*/, '').replace(/\s+$/, '')

:normalize
  // if true, clean up empty div's and remove the item if it ends up empty.
  remove\b\$
  el := @textEl
  el.normalize()
  // Remove end spaces from all the text nodes.
  dom.eachChild(el, ##)
    div\Element\$
    div.innerText = @trimEndWhitespace(div.innerText)
  // Remove empty first and last elements.
  if remove
    while el.firstChild && el.firstChild.innerText == ''
      el.removeChild(el.firstChild)
    while el.lastChild && el.lastChild.innerText == ''
      el.removeChild(el.lastChild)
  // Re-insert br tag to now empty div's.
  dom.eachChild(el, ##)
    div\Element\$
    if div.innerText == ''
      div.appendChild(dom.create('br'))
  // Remove the element if there's nothing left.
  if remove && el.childNodes.length == 0
    f_page.selection.clear()
    el.blur()
    f_page.list.remove(@)
