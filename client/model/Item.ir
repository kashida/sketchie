TODO: stop using dom element attributes and just store the properties as
member vars.

::
  // Taking the parameter as non-nullable, but the member needs to be nullable
  // for disposing.
  el\!Element\$
  @element+\Element\ el

  @bless()

:@bless
  @element.item = @
  @.isActive = true
  @element.dragger = &drag.Move(@)
  dom.listen(@element, 'click', ##)
    evt\Event\$
    if @.isActive && !evt.ctrlKey
      f_page.selection.select(@, evt.shiftKey)

:dispose
  @element.item = null
  @element.dragger = null
  @element = null

Returns Item only if the element has an Item associated to it.
:fromElement = ##
  \%.Item\
  el\!Element\$
  => \%.Item\(el.item)

:position+\data.Vec2\
  => dom.elemPosition(@element)
:positionBR+\data.Vec2\
  => dom.elemPositionBR(@element)
:position*
  p\data.Vec2\$
  @element.style.left = p.x + 'px'
  @element.style.top = p.y + 'px'
:rect+\data.Rect\
  => dom.elemRect(@element)

:rotation+\n\
  => dom.attr(@element, 'xf_r') || 0
:rotation*\n\
  r\n\$
  dom.setAttr(@element, 'xf_r', String(r))
  @setXform()

:scale+\n\
  => dom.attr(@element, 'xf_s') || 1.0
:scale*
  s\n\$
  dom.setAttr(@element, 'xf_s', String(s))
  @setXform()

:@setXform
  xforms := []
  r := @.rotation
  if r
    xforms.push('rotate(' + r + 'rad)')
  s := @.scale
  if s != 1.0
    xforms.push('scale(' + s + ')')
  @element.style['-webkit-transform'] = xforms.join(' ')

:alphaLock+\b\
  => dom.attr(@element, 'alpha_lock')
:alphaLock*
  a\b\$
  dom.setAttr(@element, 'alpha_lock', a)

:blendAlpha+\n\
  => Number(dom.attr(@element, 'blend_alpha'))
:blendAlpha*
  a\n\$
  dom.setAttr(@element, 'blend_alpha', String(a))

:isActive+\b\
  => !!@element.getAttribute('active')
:isActive*
  a\b\$
  if a
    @element.setAttribute('active', '1')
  else
    @element.removeAttribute('active')
