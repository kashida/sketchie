Translates between the three coordinate systems:
- Viewport (i.e. browser window). Top-left corner is the origin. Pixel scale.
    Rotation and scaling are done around center. This is actually the HTML
    document's coord sys.
- Document. DOM's logical coords. Calling it document although it is actually
    theb itemlist DIV's coords because it conincides with the output HTML
    document's coord system.
- Item. Top-left corner of the item is the origin. Pixel scale by default.
::

:itemToSurface = ##
  \!data.Vec2\
  p\data.Vec2\$
  c := &dom.ClientRect().center
  offset := f_viewport.xform.offset
  => &data.Vec2(#)
    p.x + offset.x - c.x + c.x / f_viewport.xform.scale
    p.y + offset.y - c.y + c.y / f_viewport.xform.scale

:viewToItem = ##
  \!data.Vec2\
  p\data.Vec2\$
  c := &dom.ClientRect().center
  offset := f_viewport.xform.offset
  => &data.Vec2(#)
    (p.x - c.x) / f_viewport.xform.scale - offset.x + c.x
    (p.y - c.y) / f_viewport.xform.scale - offset.y + c.y

:surfaceToItem = ##
  \!data.Vec2\
  p\data.Vec2\$
  c := &dom.ClientRect().center
  offset := f_viewport.xform.offset
  => &data.Vec2(#)
    p.x - offset.x + c.x - c.x / f_viewport.xform.scale
    p.y - offset.y + c.y - c.y / f_viewport.xform.scale

:item2view = ##
  \!data.Vec2\
  p\!data.Vec2\$
  xform\!dom.Xform\$
  => %.Coords.doc2view(%.Coords.item2doc(p, xform))

:item2doc = ##
  \!data.Vec2\
  p\!data.Vec2\$
  xform\!dom.Xform\$
  => #.
    p
    mult(xform.scale)
    rotate(xform.rotation)
    plus(xform.position)

:doc2view = ##
  \!data.Vec2\
  p\data.Vec2\$
  // TODO: Consider view's rotation and flipping.
  => #.
    p
    plus(f_viewport.xform.offset)
    mult(f_viewport.xform.scale)

:view2item = ##
  \!data.Vec2\
  p\!data.Vec2\$
  xform\!dom.Xform\$
  => %.Coords.doc2item(%.Coords.view2doc(p), xform)

:view2doc = ##
  \!data.Vec2\
  p\!data.Vec2\$
  // TODO: Consider view's rotation and flipping.
  => #.
    p
    div(f_viewport.xform.scale)
    minus(f_viewport.xform.offset)

:doc2item = ##
  \!data.Vec2\
  p\!data.Vec2\$
  xform\!dom.Xform\$
  => #.
    p
    minus(xform.position)
    rotate(-xform.rotation)
    div(xform.scale)
