::<drag.Dragger
  @viewport\Element\ dom.id('itemlist')
  @active\b\ true
  @drag_active\b\ false
  ^()
  @.reset()
  dom.listen(document.body, 'mousedown', ##)
    evt\Event\$
    @dragStart(evt)

:activate
  @active = true
:deactivate
  @active = false

:reset
  @scale = 1.0
  @offset_x = 0
  @offset_y = 0
  @setViewport()

:scale
  => @scale

:offset\data.Vec2\
  => &data.Vec2(@offset_x, @offset_y)

:itemToView\data.Vec2\
  x\n\$
  y\n\$
  half_w := @viewport.offsetWidth / 2
  half_h := @viewport.offsetHeight / 2
  => &data.Vec2(#)
    (x + @offset_x - half_w) * @scale + half_w
    (y + @offset_y - half_h) * @scale + half_h

:itemToSurface\data.Vec2\
  p\data.Vec2\$
  half_w := @viewport.offsetWidth / 2
  half_h := @viewport.offsetHeight / 2
  => &data.Vec2(#)
    p.x + @offset_x - half_w + half_w / @scale
    p.y + @offset_y - half_h + half_h / @scale

:@viewToItem\data.Vec2\
  p\data.Vec2\$
  half_w := @viewport.offsetWidth / 2
  half_h := @viewport.offsetHeight / 2
  => &data.Vec2(#)
    (p.x - half_w) / @scale - @offset_x + half_w
    (p.y - half_h) / @scale - @offset_y + half_h

:surfaceToItem\data.Vec2\
  p\data.Vec2\$
  half_w := @viewport.offsetWidth / 2
  half_h := @viewport.offsetHeight / 2
  => &data.Vec2(#)
    p.x - @offset_x + half_w - half_w / @scale
    p.y - @offset_y + half_h - half_h / @scale

incr should be -1, 0, or 1 where positive is zooming in
:zoom
  incr\n\$
  if incr > 0
    @scale = @scale * 1.5
  if incr < 0
    @scale = @scale / 1.5
  @setViewport()

:move
  dx\n\$
  dy\n\$
  w := @viewport.offsetWidth
  h := @viewport.offsetHeight
  @offset_x += dx * @viewport.clientWidth / 3 / @scale
  @offset_y += dy * @viewport.clientHeight / 3 / @scale
  @setViewport()

Change offset (but not scaling) to put the item in view.
:showItem
  item\model.Item\$
  // Viewport and the item in item coord space.
  vp0 := @viewToItem(&data.Vec2(0, 0))
  vp1 := @viewToItem(&data.Vec2(@viewport.offsetWidth, @viewport.offsetHeight))
  p0 := item.position
  p1 := item.positionBR

  // Find center positin delta.
  dcx := (vp0.x + vp1.x - p0.x - p1.x) * 0.5
  dcy := (vp0.y + vp1.y - p0.y - p1.y) * 0.5
  // Find the minimum distance we need to move to get the item in the view.
  @offset_x += #?
    dcx > 0
    inrange(0, dcx, vp0.x - p0.x)
    inrange(dcx, 0, vp1.x - p1.x)
  @offset_y += #?
    dcy > 0
    inrange(0, dcy, vp0.y - p0.y)
    inrange(dcy, 0, vp1.y - p1.y)
  @setViewport()

Change scaling and offset to put all the items in view.
:showAllItems
  union := &pixel.UnionRect()
  union.add(toArray(f_page.items.children))
  r := union.rect
  vw := @viewport.offsetWidth
  vh := @viewport.offsetHeight
  @scale = r.w / r.h > vw / vh ? vw / r.w : vh / r.h
  @offset_x = -r.x0 - r.w * 0.5 + vw * 0.5
  @offset_y = -r.y0 - r.h * 0.5 + vh * 0.5
  @setViewport()

:@setViewport
  @viewport.style['-webkit-transform'] = [#].join(' ')
    'scale(' + @scale + ')'
    'translate(' + Math.floor(@offset_x) + 'px, ' + Math.floor(@offset_y) + 'px)'

:copyTo
  div\model.Item\$
  div.style['-webkit-transform'] = @viewport.style['-webkit-transform']

:@dragStart
  evt\Event\$
  if !@active
    =>
  if !evt.ctrlKey
    =>
  @mouse_x = evt.clientX
  @mouse_y = evt.clientY
  @base_x = @offset_x
  @base_y = @offset_y
  f_dispatcher.dragStart(@)
  evt.preventDefault()
  => true

:<dragUpdate
  evt\Event\$
  if !@active
    =>
  @offset_x = @base_x + (evt.clientX - @mouse_x) / @scale
  @offset_y = @base_y + (evt.clientY - @mouse_y) / @scale
  @setViewport()

:<dragEnd
  evt\Event\$
  if !@active
    =>
  @.dragUpdate(evt)
