::^$drag.Dragger
  @element\Element\ $dom.id('itemlist')
  @onChange+\NullEvent\ &NullEvent()
  @xform+\\ $dom.ViewportXform(@element, @onChange)
  @active\b\ true
  @drag_active\b\ false
  @mouse_pos\\ $data.Vec2(0, 0)
  @base_pos\\ $data.Vec2(0, 0)
  ^()
  @xform.reset()
  $dom.listen(\Element\ (document.body), 'mousedown', ##)
    evt\Event\$
    @dragStart(evt)

:activate
  @active = true
:deactivate
  @active = false

Change offset (but not scaling) to put the item in view.
:showItem
  item\$model.Item\$
  // Viewport and the item in item coord space.
  cr := &$dom.ClientRect()
  vp0 := $ui.Coords.viewToItem(cr.p00)
  vp1 := $ui.Coords.viewToItem(cr.p11)
  p0 := item.xform.position
  p1 := item.xform.positionBR

  // Find center positin delta.
  dcx := (vp0.x + vp1.x - p0.x - p1.x) * 0.5
  dcy := (vp0.y + vp1.y - p0.y - p1.y) * 0.5
  // Find the minimum distance we need to move to get the item in the view.
  @xform.move(#)
    dcx > 0 ? inrange(0, dcx, vp0.x - p0.x) : inrange(dcx, 0, vp1.x - p1.x)
    dcy > 0 ? inrange(0, dcy, vp0.y - p0.y) : inrange(dcy, 0, vp1.y - p1.y)

Change scaling and offset to put all the items in view.
:showAllItems
  union := &$pixel.UnionRect()
  union.add(f_page.list.childrenArray)
  r := union.rect
  cr := &$dom.ClientRect()
  @xform.set(#)
    r.w / r.h > cr.w / cr.h ? cr.w / r.w : cr.h / r.h
    -r.x0 - r.w * 0.5 + cr.w * 0.5
    -r.y0 - r.h * 0.5 + cr.h * 0.5

:copyTo
  div\$model.Item\$
  div.element.style['-webkit-transform'] = @element.style['-webkit-transform']

:@dragStart
  evt\Event\$
  if !@active
    =>
  if !evt.ctrlKey
    =>
  @mouse_pos = &$data.Vec2(evt.clientX, evt.clientY)
  @base_pos = @xform.offset.clone
  f_dispatcher.dragStart(@)
  evt.preventDefault()
  => true

:dragUpdate^
  evt\Event\$
  if !@active
    =>
  @xform.setOffset(#)
    @base_pos.x + (evt.clientX - @mouse_pos.x) / @xform.scale
    @base_pos.y + (evt.clientY - @mouse_pos.y) / @xform.scale

:dragEnd^
  evt\Event\$
  if !@active
    =>
  @.dragUpdate(evt)
