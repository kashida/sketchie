Canvas that's fixed to the viewport size.
::
  canvas_or_w\HTMLCanvasElement|n\$
  h\n\?
  @canvas\!dom.Canvas\ &dom.Canvas(canvas_or_w, h)

:element+\HTMLCanvasElement\
  => @canvas.element

:context+\CanvasRenderingContext2D\
  => @canvas.ctx

:duplicateCanvas\!HTMLCanvasElement\
  => @canvas.duplicate()

:copyFrom
  cvs\!HTMLCanvasElement\$
  @canvas.copyFrom(cvs)

:clear
  @canvas.clear()

:pixelData+\!pixel.Data\
  => @canvas.pixelData

:attachToImage
  img\!model.Image\$
  img.element.appendChild(@canvas.element)

:attachBeforeElement
  ref_el\Element\$
  ref_el.parentNode.insertBefore(@canvas.element, ref_el)

:detach
  @canvas.detach()

:activate
  img\model.Image\$
  drawImage\b\? true
  @canvas.show()
  // TODO: This save probably needs to be done after canvas.setToRect is
  // called (which resets the context).
  @canvas.ctx.save()
  if img
    @setCanvasToCoverViewport(img)
    if drawImage
      @canvas.ctx.drawImage(img.img, 0, 0)
    @setCanvasXformToItem(img)
    @setCanvasXformToViewport()
  else
    @canvas.setToWindowSize()
  @.updateAlphaLock(!!img && img.alphaLock)

:@setCanvasToCoverViewport
  img\!model.Image\$
  // Item coordinates bbox of the viewport.
  bbox := null
  &dom.ClientRect().points.forEach(##)
    p\!data.Vec2\$
    item_p := ui.Coords.view2item(p, \!dom.Xform\(img.xform))
    if bbox
      bbox.expandToInclude(item_p)
    else
      bbox = &data.Rect(item_p.x, item_p.y, item_p.x, item_p.y)
  @canvas.setToRect(\!data.Rect\(bbox))
  // Adjust the canvas coords for the difference.
  @canvas.ctx.translate(-bbox.x0, -bbox.y0)

:@setCanvasXformToItem
  img\!model.Image\$
  p := img.xform.position
  @canvas.ctx.translate(-p.x, -p.y)

:@setCanvasXformToViewport
  // Need to match the backing store resolution with logical resolution.
  //viewport_scale := f_viewport.scale
  //viewport_scale_inverse := 1.0 / viewport_scale
  //@canvas.ctx.scale(viewport_scale_inverse, viewport_scale_inverse)
  p := f_viewport.offset
  @canvas.ctx.translate(-p.x, -p.y)

:updateAlphaLock
  alpha_lock\b\$
  @canvas.ctx.globalCompositeOperation = alpha_lock ? 'source-atop' : 'source-over'

:deactivate
  @canvas.clear()
  @canvas.ctx.restore()
  @canvas.hide()
  @canvas.ctx.globalCompositeOperation = ''

Val is the opacity value [0, 1.0], but resets to default when null.
:opacity*
  val\?s|n\$
  @canvas.opacity = val

Takes view coordinates and returns [r, g, b, a] array in [0, 255] range.
:pickColor\data.Color\
  x\n\$
  y\n\$
  scale := f_viewport.scale
  pixels := @canvas.ctx.getImageData(x / scale, y / scale, 1, 1)
  d := pixels.data
  => data.Color.fromByteRange(d[0], d[1], d[2], d[3])

:imageSpec\pixel.PositionedPixels\
  orig_img\!model.Image\?
  // Produce image spec of this surface plus the original image.
  // Size of the image is reduced to the tight bound.
  pixelData := @canvas.pixelData
  => #?
    orig_img
    &pixel.Merge(pixelData, orig_img).pixels()
    pixelData.positioned()
