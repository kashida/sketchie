TODO:
- Command to send events to any elem (mainly mouse click).
- Command to wait (timeout with 0?).
- Command to assert / check dom states.

::TestBase
  @name\s\$
  @success\b\ true
  @testName\s\ ''

:run
  @init()
  @runTests()
  @report(@success ? 'GOOD' : 'BAD')

:@init
  window.onerror = ##
    err\s\$
    file\s\$
    line\n\$
    @success = false
    @report(#)
      'error: ' + err
      '    ' + file + ' (line ' + line + ')'
    => true

:@runTests
  // run all the methods that has names starting with 'test'.
  each m in @
    if /^test/.test(m) && typeof(@[m]) == 'function'
      @testName = m
      try
        @[m].apply(@)
      catch e
        @success = false
        @reportException(e)
      @testName = ''

:@sendKeyCode
  code\n\$
  shift\b\? false
  // There seems to be webkit bug that KeyboardEvent not allowing keyCode to be set.
  e := &Event('keyup')
  e.keyCode = code
  e.shiftKey = shift
  e.target = document.body
  document.body.dispatchEvent(e)

:@sendKeys
  kstr\s\$
  shift\b\? false
  kstr.split('').forEach(##)
    kchar\s\$
    @sendKeyCode(c(kchar.toUpperCase()), shift)

:@sendInput
  str\s\$
  sel := window.getSelection()
  node := sel.anchorNode
  offset := sel.anchorOffset
  if node instanceof Text
    current_str := node.wholeText
    node.replaceWholeText(#)
      current_str.substr(0, offset)
      str
      current_str.substr(offset)
    sel.collapse(node, offset + str.length)
  else
    node.innerText = str

:@select
  sel\s\$
  => document.querySelector(sel)

:@verify
  cond\b\$
  if cond
    =>
  @success = false
  try
    // Make an error to get the stack trace.
    throw &Error()
  catch e
    @reportException(e, 'failure')

:@report
  line\s\$
  desc\s\? ''
  xhrPost('/_tr', '* [' + # + '] ' + line + #)
    @testName ? @name + '.' + @testName : @name
    desc ? '\n' + desc : ''

:@reportException
  e\Error\$
  title\s\?
  stack_lines := e.stack.split('\n')
  title_line = title || stack_lines[0]
  @report(#)
    title_line + ':'
    stack_lines.slice(1).filter(##).join('\n')
      line\s\$
      // Dropping the stacks that belong to this class.
      => !/^\s*at\s+TestBase\./.test(line)
